import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.jetbrains.kotlin.jvm' version '1.9.23'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.23'
	id "org.jlleitschuh.gradle.ktlint" version "12.1.0"
	id "org.flywaydb.flyway" version "10.11.1"
}


group = 'com.giftandgo'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}


sourceSets {
	integrationTest {
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}
}

tasks.register('integrationTest', Test) {
	description = "Run integration tests"
	group = "verification"
	useJUnitPlatform()
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	systemProperty('spring.profiles.active', 'integration')
}

configurations {
	integrationTestImplementation.extendsFrom configurations.testImplementation
	integrationTestRuntimeOnly.extendsFrom configurations.testRuntimeOnly
	integrationTestCompileOnly.extendsFrom configurations.testCompileOnly
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.data:spring-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.17.0'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8'
	implementation 'io.konform:konform-jvm:0.4.0'
	implementation 'com.mysql:mysql-connector-j'
	implementation 'org.flywaydb:flyway-core:10.11.1'
	implementation 'org.flywaydb:flyway-mysql:10.11.1'



	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'io.kotest:kotest-assertions-core-jvm:5.8.1'
	testImplementation 'org.wiremock:wiremock-jetty12:3.5.4'
	testImplementation 'com.marcinziolo:kotlin-wiremock:2.1.1'
}

tasks.withType(KotlinCompile) {
	kotlinOptions {
		freeCompilerArgs += '-Xjsr305=strict'
		jvmTarget = '17'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

